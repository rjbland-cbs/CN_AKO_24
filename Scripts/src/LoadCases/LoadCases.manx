PROCESS LoadCases

SETTINGS
   AUTOREAD=NO
   CONNECT=NO

USES
   DATAMODEL CMA_Loader
   FIELDS
      MainSurveyID : STRING[36]
      SurveyDisplayName : STRING[30]
      ID : STRING[219]
      CMA_ForWhom : STRING[20]
      CMA_AllowSpawning : (no (0), yes), EMPTY
      Name : STRING[50], EMPTY
      Address : STRING[50], EMPTY
      PostalCode : STRING[20], EMPTY
      GemeenteCode : STRING[4], EMPTY
      BuurtOmschrijving : STRING[30], EMPTY
      Town : STRING[50], EMPTY
      Phone1 : STRING[20], EMPTY
      CaseNote : STRING
      Image : STRING[30]
      GeoLocation : STRING[30]
   ENDMODEL

   Launcher 'Data/Launcher'
   Attempts 'Data/CN_Attempts'

INPUTFILE inp : CMA_Loader (ASCII)
SETTINGS
   SEPARATOR=';'
   DELIMITER='"'
   HEADERLINE=YES
   CHARACTERSET=UTF8
   OPEN=NO

//UPDATEFILE up1 : Launcher ('../Data/Launcher', BDIX)
SURVEYDATAFILE up1 : Launcher

AUXFIELDS
   Res : INTEGER
   aAdded, aExisted : INTEGER
   
FUNCTION OpenInputFile : INTEGER
AUXFIELDS
   aFileName : OPEN
INSTRUCTIONS
   aFileName := SELECTFILE('Steekproefbestand', '', 'CSV File (*.csv)|*.csv')
   AddToLog('Stp: ' + aFileName)
   RESULT := inp.OPEN(aFileName)
   AddToLog('Open: ' + STR(RESULT))
ENDFUNCTION

FUNCTION ExtractFilePath: OPEN
PARAMETERS pFilename: OPEN
INSTRUCTIONS
  RESULT:= REGEXSTRING('.*(\\\\|/)',pFileName)
ENDFUNCTION 


INCLUDE "../Include/Shared.incx"
INCLUDE "../Include/Login.incx"


PROCEDURE Main
INSTRUCTIONS
   NewLog
   IF OpenInputFile = 0 THEN
      DISPLAY('Processing cases', HOURGLASS)
      REPEAT
         inp.READNEXT
         AddToLog(STR(inp.RECORDNUMBER) + ': ' + [[inp.ToJson(FALSE)]])
         up1.GET(inp.MainSurveyId, inp.Id)
         IF NOT up1.RESULTOK THEN
            
            up1.INITRECORD
            up1.MainSurveyID := inp.MainSurveyId
            up1.SurveyDisplayName := inp.SurveyDisplayName
            up1.ID := inp.ID
            up1.CMA_ForWhom := inp.CMA_ForWhom
            up1.CMA_AllowSpawning := inp.CMA_AllowSpawning
            up1.CaseNote := inp.CaseNote
            
            up1.CMA_Process.CreatedDT := TIMETOSTR(SYSTIME, 'yyyyMMdd,HH:mm:sszzz')
         
            up1.CMA_ContactData := ''
            up1.CMA_ContactData := up1.CMA_ContactData + 'pii.name'       + CHAR(9) + name              + CHAR(9)
            up1.CMA_ContactData := up1.CMA_ContactData + 'pii.address'    + CHAR(9) + address           + CHAR(9)
            up1.CMA_ContactData := up1.CMA_ContactData + 'pii.postalcode' + CHAR(9) + postalcode        + CHAR(9)
            up1.CMA_ContactData := up1.CMA_ContactData + 'pii.town'       + CHAR(9) + buurtomschrijving + CHAR(9)
         
            up1.CMA_ContactData := up1.CMA_ContactData + 'pii.phone1'     + CHAR(9) + phone1            + CHAR(9)
            up1.CMA_ContactData := up1.CMA_ContactData + 'pii.phone2'     + CHAR(9)                     + CHAR(9)
            up1.CMA_ContactData := up1.CMA_ContactData + 'casenote'       + CHAR(9) + casenote          + CHAR(9)
            up1.CMA_ContactData := up1.CMA_ContactData + 'mainsurveyid'   + CHAR(9) + mainsurveyid      + CHAR(9)
            up1.CMA_ContactData := up1.CMA_ContactData + 'id'             + CHAR(9) + id                + CHAR(9)
         
            IF inp.Image<>'' THEN
               Res := up1.CMA_ContactImage.LOADFROMDISK('Images\\' + Image)
            ENDIF
         
            up1.CMA_GeoLocation := GeoLocation
         
            up1.CMA_AttemptsGUID := [[Attempts.InstrumentId]]
         
            up1.ContactInfoShort := Address
         
            IF inp.BuurtOmschrijving <> EMPTY THEN
               up1.ContactInfoShort := up1.ContactInfoShort + ',' + BuurtOmschrijving
            ELSEIF inp.PostalCode <> EMPTY THEN
               up1.ContactInfoShort := up1.ContactInfoShort + ',' + PostalCode
            ENDIF
         
            IF inp.Name <> EMPTY THEN
               up1.ContactInfoShort := up1.ContactInfoShort + ',' + Name
            ENDIF
         
            up1.CMA_PreLoadForEdit := '"' + 'Main.NAW.gemeente_code=' + GemeenteCode      +
                                      ',' + 'Main.NAW.postcode='      + PostalCode        +
                                      ',' + 'Main.NAW.straatnaam='    + Address           +
                                      ',' + 'Main.NAW.plaatsnaam='    + BuurtOmschrijving +
                                      '"'
         
            up1.CHECKRULES
            up1.WRITE
            aAdded := aAdded + 1
         ELSE // case exists
            AddToLog('Case ' + [[up1.Keys.GetItem('Primary').KeyValue]] + ' already exists')
            aExisted := aExisted + 1
         ENDIF
      UNTIL inp.LASTRECORD
      CLEARDISPLAY
   ENDIF
ENDPROCEDURE

PROCEDURE ConnectToData
INSTRUCTIONS
   up1.SETCONNECTIONSTRING(GetConnectionString([[Launcher]], 'CMA'))
   IF up1.OPEN = 0 THEN auxConnected := 1 ENDIF
   
ENDPROCEDURE

MANIPULATE
   Login
   IF auxLoggedIn = 1 AND auxConnected = 1 THEN
      Main
   ENDIF