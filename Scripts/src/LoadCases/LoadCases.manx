
SETTINGS
   AUTOREAD=NO
   CONNECT=NO

USES
   DATAMODEL CMA_Loader
   FIELDS
      MainSurveyID : STRING[36]
      SurveyDisplayName : STRING[30]
      ID : STRING[219]
      CMA_ForWhom : STRING[20]
      CMA_AllowSpawning : (no (0), yes), EMPTY
      Name : STRING[50], EMPTY
      Address : STRING[50], EMPTY
      PostalCode : STRING[20], EMPTY
      GemeenteCode : STRING[4], EMPTY
      BuurtOmschrijving : STRING[30], EMPTY
      Town : STRING[50], EMPTY
      Phone1 : STRING[20], EMPTY
      CaseNote : STRING
      Image : STRING[30]
      GeoLocation : STRING[30]
   ENDMODEL

   Launcher '../Data/Launcher'
   Attempts '../Data/CN_Attempts'

INPUTFILE inp : CMA_Loader (ASCII)
SETTINGS
   SEPARATOR=';'
   DELIMITER='"'
   HEADERLINE=YES
   CHARACTERSET=UTF8
   OPEN=NO

UPDATEFILE up1 : Launcher ('../Data/Launcher', BDIX)

AUXFIELDS
   Res : INTEGER
   
FUNCTION OpenInputFile : INTEGER
AUXFIELDS
   aFileName : OPEN
INSTRUCTIONS
   ReadCache
   aFileName := SELECTFILE('Steekproefbestand', tCache.StpFilePath, 'CSV File (*.csv)|*.csv')
   AddToLog('Stp: ' + aFileName)
   RESULT := inp.OPEN(aFileName)
   AddToLog('Open: ' + STR(RESULT))
ENDFUNCTION

FUNCTION ExtractFilePath: OPEN
PARAMETERS pFilename: OPEN
INSTRUCTIONS
  RESULT:= REGEXSTRING('.*(\\\\|/)',pFileName)
ENDFUNCTION 

PROCEDURE CacheFilePath
INSTRUCTIONS
   tCache.StpFilePath := inp.PATHNAME
   WriteCache
ENDPROCEDURE

INCLUDE "../Shared.incx"

MANIPULATE
   NewLog
   IF OpenInputFile = 0 THEN
      CacheFilePath
      DISPLAY('Processing cases', HOURGLASS)
      REPEAT
         inp.READNEXT
         AddToLog(STR(inp.RECORDNUMBER) + ': ' + [[inp.ToJson(FALSE)]])
         up1.GET(inp.MainSurveyId, inp.Id)
         IF NOT up1.RESULTOK THEN
            
            up1.INITRECORD
            up1.MainSurveyID := inp.MainSurveyId
            up1.SurveyDisplayName := inp.SurveyDisplayName
            up1.ID := inp.ID
            up1.CMA_ForWhom := inp.CMA_ForWhom
            up1.CMA_AllowSpawning := inp.CMA_AllowSpawning
            up1.CaseNote := inp.CaseNote
            
            up1.CMA_Process.CreatedDT := TIMETOSTR(SYSTIME, 'yyyyMMdd,HH:mm:sszzz')
         
            up1.CMA_ContactData := ''
            up1.CMA_ContactData := CMA_ContactData + 'pii.name'       + CHAR(9) + name              + CHAR(9)
            up1.CMA_ContactData := CMA_ContactData + 'pii.address'    + CHAR(9) + address           + CHAR(9)
            up1.CMA_ContactData := CMA_ContactData + 'pii.postalcode' + CHAR(9) + postalcode        + CHAR(9)
            up1.CMA_ContactData := CMA_ContactData + 'pii.town'       + CHAR(9) + buurtomschrijving + CHAR(9)
         
            up1.CMA_ContactData := CMA_ContactData + 'pii.phone1'     + CHAR(9) + phone1            + CHAR(9)
            up1.CMA_ContactData := CMA_ContactData + 'pii.phone2'     + CHAR(9)                     + CHAR(9)
            up1.CMA_ContactData := CMA_ContactData + 'casenote'       + CHAR(9) + casenote          + CHAR(9)
            up1.CMA_ContactData := CMA_ContactData + 'mainsurveyid'   + CHAR(9) + mainsurveyid      + CHAR(9)
            up1.CMA_ContactData := CMA_ContactData + 'id'             + CHAR(9) + id                + CHAR(9)
         
            IF inp.Image<>'' THEN
               Res := up1.CMA_ContactImage.LOADFROMDISK('Images\\' + Image)
            ENDIF
         
            up1.CMA_GeoLocation := GeoLocation
         
            up1.CMA_AttemptsGUID := [[Attempts.InstrumentId]]
         
            up1.ContactInfoShort := Address
         
            IF inp.BuurtOmschrijving <> EMPTY THEN
               up1.ContactInfoShort := ContactInfoShort + ',' + BuurtOmschrijving
            ELSEIF inp.PostalCode <> EMPTY THEN
               up1.ContactInfoShort := ContactInfoShort + ',' + PostalCode
            ENDIF
         
            IF inp.Name <> EMPTY THEN
               up1.ContactInfoShort := up1.ContactInfoShort + ',' + Name
            ENDIF
         
            up1.CMA_PreLoadForEdit := '"' + 'Main.NAW.gemeente_code=' + GemeenteCode      +
                                      ',' + 'Main.NAW.postcode='      + PostalCode        +
                                      ',' + 'Main.NAW.straatnaam='    + Address           +
                                      ',' + 'Main.NAW.plaatsnaam='    + BuurtOmschrijving +
                                      '"'
         
            up1.CHECKRULES
            up1.WRITE
         ELSE // case exists
            AddToLog('Case ' + [[up1.Keys.GetItem('Primary').KeyValue]] + ' already exists')
            DISPLAY('Case ' + [[up1.Keys.GetItem('Primary').KeyValue]] + ' bestaat al in de launcher database', WAIT)
         ENDIF
      UNTIL inp.LASTRECORD
      CLEARDISPLAY
   ENDIF
