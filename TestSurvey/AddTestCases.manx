SETTINGS
   AUTOREAD = NO
   CONNECT = NO

{
'C:\Blaise5\Surveys\
}

USES
   mTopic 'TestSurvey'
   mLauncher 'Data\Launcher'
   mAttempts 'Data\CN_Attempts'
   mManageInfo 'Data\CMA_ManageInfo'
   mUserInfo 'Data\CMA_UserInfo'

UPDATEFILE uLauncher : mLauncher('Data\Launcher', BDIX)
UPDATEFILE uManageInfo : mManageInfo('Data\CMA_ManageInfo', BDIX)
UPDATEFILE uUserInfo : mUserInfo('Data\CMA_UserInfo', BDIX)

CONST
   cSupervisor = 'CMA_Sup'
   cCasesPerUser = 20

AUXFIELDS
   i : INTEGER
   Res : INTEGER 
   aTab : STRING
   aFactor : INTEGER
   
PROCEDURE AddUser
PARAMETERS
   pUserId : STRING
   pRole : (Interviewer, Supervisor)
AUXFIELDS i : INTEGER
INSTRUCTIONS
   IF pRole = Interviewer THEN
      FOR i := aFactor * cCasesPerUser + 1 TO (aFactor + 1) * cCasesPerUser  DO
         uLauncher.INITRECORD
         uLauncher.MainSurveyID := [[mTopic.InstrumentId]]
         uLauncher.ID := STR(i)
         uLauncher.CMA_Process.CreatedDT := DATETOSTR(SYSDATETIME, 'yyyyMMdd,HH:mm:sszzz')
         uLauncher.SurveyDisplayName := [[mTopic.Name]]
         uLauncher.CMA_StartDate := SYSDATE
         uLauncher.CMA_EndDate := SYSDATE + 28
         
         uLauncher.CMA_AllowSpawning := yes
         
         uLauncher.CMA_ForWhom := pUserId
         
         uLauncher.CMA_AttemptsGUID := [[mAttempts.InstrumentId]]
         
         uLauncher.CMA_ContactData := 'pii.name' + aTab + 'Name' + STR(i) + aTab
         uLauncher.CMA_ContactData := CMA_ContactData + 'pii.Address' + aTab + 'Address' + STR(i) + aTab
         uLauncher.CMA_ContactData := CMA_ContactData + 'pii.PostalCode' + aTab + '12345' + aTab
         uLauncher.CMA_ContactData := CMA_ContactData + 'pii.Town' + aTab + 'FakeTown' + aTab
         uLauncher.CMA_ContactData := CMA_ContactData + 'pii.Phone1' + aTab + '' + aTab
         uLauncher.CMA_ContactData := CMA_ContactData + 'pii.Phone2' + aTab + '' + aTab
         uLauncher.CMA_ContactData := CMA_ContactData + 'casenote' + aTab + '' + aTab
         uLauncher.CMA_ContactData := CMA_ContactData + 'mainsurveyid' + aTab + [[mTopic.InstrumentId]] + aTab
         uLauncher.CMA_ContactData := CMA_ContactData + 'id' + aTab + STR(i) + aTab
         
         uLauncher.ContactInfoShort := 'Test Case ' + STR(i)
         uLauncher.WRITE
      ENDDO
      aFactor := aFactor + 1
   ENDIF
   
   uUserInfo.INITRECORD
   uUserInfo.UserId := pUserId
   IF pRole = Interviewer THEN
      uUserInfo.UserType := Interviewer
      uUserInfo.ManagerId := cSupervisor
   ELSE
      uUserInfo.UserType := Supervisor
   ENDIF
   uUserInfo.WRITE
ENDPROCEDURE
   
MANIPULATE
   // CMA_Launcher and CMA_UserInfo
   aTab := CHAR(9)
   AddUser('CMA_Chuck', Interviewer)
   
   // CMA_ManageInfo
   uManageInfo.INITRECORD
   uManageInfo.UserId := 'CMA_Sup'
   uManageInfo.InstrumentId := [[mTopic.InstrumentId]]
   uManageInfo.SurveyName := [[mTopic.Name]]
   uManageInfo.write
   